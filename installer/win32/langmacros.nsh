;;
;; Windows INPTOOLS NSIS installer language macros
;;

!macro INPTOOLS_MACRO_DEFAULT_STRING LABEL VALUE
  !ifndef "${LABEL}"
    !define "${LABEL}" "${VALUE}"
    !ifdef INSERT_DEFAULT
      !warning "${LANG} lang file missing ${LABEL}, using default.."
    !endif
  !endif
!macroend

!macro INPTOOLS_MACRO_LANGSTRING_INSERT LABEL LANG
  LangString "${LABEL}" "${LANG_${LANG}}" "${${LABEL}}"
  !undef "${LABEL}"
!macroend

!macro INPTOOLS_MACRO_LANGUAGEFILE_BEGIN LANG
  !define CUR_LANG "${LANG}"
!macroend

!macro INPTOOLS_MACRO_LANGUAGEFILE_END
  !define INSERT_DEFAULT
  !include "${INPTOOLS_DEFAULT_LANGFILE}"
  !undef INSERT_DEFAULT

  ; String labels should match those from the default language file.
  
  !insertmacro INPTOOLS_MACRO_LANGSTRING_INSERT INPTOOLS_OPEN_WITH_EPANET ${CUR_LANG}
  !insertmacro INPTOOLS_MACRO_LANGSTRING_INSERT INPTOOLS_CREATE_GERMAN_REPORT ${CUR_LANG}
  !insertmacro INPTOOLS_MACRO_LANGSTRING_INSERT INPTOOLS_PROJECT_GK3_WGS84 ${CUR_LANG}
  !insertmacro INPTOOLS_MACRO_LANGSTRING_INSERT INPTOOLS_CREATE_CSV ${CUR_LANG}
  !insertmacro INPTOOLS_MACRO_LANGSTRING_INSERT INPTOOLS_CREATE_BINARY ${CUR_LANG}
  !insertmacro INPTOOLS_MACRO_LANGSTRING_INSERT INPTOOLS_HELP ${CUR_LANG}
  !insertmacro INPTOOLS_MACRO_LANGSTRING_INSERT INPTOOLS_EPANET_HELP ${CUR_LANG}
  !insertmacro INPTOOLS_MACRO_LANGSTRING_INSERT INPTOOLS_ABOUT ${CUR_LANG}

  !undef CUR_LANG
!macroend

!macro INPTOOLS_MACRO_INCLUDE_LANGFILE LANG FILE
  !insertmacro INPTOOLS_MACRO_LANGUAGEFILE_BEGIN "${LANG}"
  !include "${FILE}"
  !insertmacro INPTOOLS_MACRO_LANGUAGEFILE_END
!macroend
